[toc]

# 列表操作

## Cons 和 Append

1. Cons 构造：将一个元素添加到列表中
   - 使用：`a : list`
2. Append 追加：将一个列表添加到另一个列表中，合并两个列表
   - 使用：`list1 ++ list2`

```haskell
-- Cons
ghci> 1 : [2, 3]
[1,2,3]
ghci> 'l' : "ist"
"list"
-- 也可以递归着使用Cons
ghci> 1 : (2 : (3 : []))
[1,2,3]

-- Append
ghci> [1] ++ [2, 3]
[1,2,3]
ghci> "li" ++ "st"
"list"
```

## Cons

### 列表中的元素操作

1. 获取第一个元素：`head list`
2. 获取除第一个元素外剩下的所有元素：`tail list`
3. 判断列表是不是空的：`null list`

```haskell
ghci> head [1, 2, 3]
1
ghci> tail [1, 2, 3]
[2,3]
ghci> null []
True
```

### Cons和递归

因为有 Cons，所以，我们可以这么理解一个列表。

对于任何列表

1. 要么空，要么非空
2. 每次递归都可以分解为 `一个元素: 剩余列表` 的形式
   - 比如一个列表[1, 2, 3]，其实就是 `1 : (2 : (3 : []))`
   - 第一次递归：`[1 : ...]`
   - 第二次：`[2 : ...]`
   - 直到递归到 `[]` 为空时，结束递归

例一：计算列表中每个元素的平方

```haskell
-- 一、计算列表中每个元素的平方
-- 方法1： 使用之前的方法
squares :: [Int] -> [Int]
squares l = [x^2 | x <- l]


-- 方法二：定义函数
-- 实现递归遍历列表，并将每个元素求平方
-- 首先先声明函数，并指出参数为int列表，返回一个int列表
squaresCond :: [Int] -> [Int]
squaresCond l = 
    -- 判空操作：如果列表为空，直接返回空列表
    -- 作用：防止原始列表就为空，另一个是递归到最后为[]空时跳出递归
    if null l then []
    -- 如果列表不为空，开始递归操作
    else 
        -- 分割当前递归下的列表，头部元素和剩余元素列表
        let x = head l
            xs = tail l
        -- 头部元素执行平方操作，剩余元素列表开始递归
        in
            x^2 : squaresCond xs

-- 方法三：也可以如下实现递归
squaresRec :: [Int] -> [Int]
-- 判空操作：如果列表为空，直接返回空列表，作用见上
squaresRec [] = []
-- 如果列表不为空，开始递归操作
-- 将列表分解为头部元素 x 和剩余元素列表 xs
-- 然后头部元素取平方，剩余列表列表 xs 执行递归，调用 squaresRec 函数
squaresRec (x : l) = x^2 : squaresRec l
```

例二：输出列表全部奇数

```haskell
-- 二、过滤列表中的奇数
-- 方法一：使用守卫的方法
odds :: [Int] -> [Int]
odds l = [x | x <- l, odd x]

-- 方法二：递归
oddsCond :: [Int] -> [Int]
oddsCond l =
    -- 判空
    if null l then []
    else
        -- 分割
        let
            x = head l
            xs = tail l
        in
            -- 如果是奇数，头部元素输出，剩余列表递归
            if odd x then x : oddsCond xs
            -- 如果不是奇数，直接递归剩余列表
            else oddsCond xs

-- 方法三：另一种递归
oddsRec :: [Int] -> [Int]
-- 判空
oddsRec [] = []
-- 分割
oddsRec (x : xs)
    -- 守卫来判断是否是奇数
    -- 如果是，首元素输出，剩余列表递归 
    | odd x = x : oddsRec xs
    -- 如果不是，剩余列表递归
    | otherwise = oddsRec xs
    
-- 测试
ghci> oddsCond [1, 2, 3, 4, 5, 6, 7]
[1,3,5,7]
ghci> oddsRec [1, 2, 3, 4, 5, 6, 7]
[1,3,5,7]
```

## Append

### 实现原理

```haskell
-- Append用于两个列表合并，原理实现上却是用了Con和递归
-- 重点分析前面的列表
-- 1 如果前面列表为空列表，返回后面列表
[] ++ l = l
-- 2. 如果非空，原理上：
-- 首先，对前面列表做递归执行Con分割，遍历所有元素后最后会有个[]空列表
-- 然后执行1，[] ++ l = l
-- 再从分割后的尾部元素一个个将元素Con添加到后面列表
(x : xs) ++ l = x : (xs ++ l) -- 即把前面列表分割为：(x1 : (x2 : (x3 : ....(xn : ([] ++ l)))
-- 可见，Append的算法复杂度由前面列表的长度决定，越长，越复杂
```

### 运算-结合律

```haskell
-- 1 左append等同于右append
(l1 ++ l2) ++ l3 == l1 ++ (l2 ++ l3)

-- 2 但是性能不一样，一般优先使用右append，因为原理已经说明前面列表长度决定了复杂度
-- 如：左append
((l1 ++ l2) ++ l3) ++ l4
-- 左边元素数量：n1 + (n1 + n2) + (n1 + n2 + n3) = 3n1 + 2n2 + n3

-- 若是右append
l1 ++ (l2 ++ (l3 ++ l4))
-- 左边元素数量为：n3 + n2 + n1

-- 当有m个长度为n的列表时，左：m^2 * n	右：m * n

-- 3 使用并行Append，性能更佳
-- 若有m个列表，两者性能：m-1		log m
x1 + (x2 + (x3 + (x4 + (x5 + (x6 + (x7 + x8)))))) == ((x1+x2) + (x3+x4)) + ((x5+x6) + (x7+x8))
```

### 列表操作不满足分配律

列表的Append不满足交换律，但数值的加法满足

```haskell
-- 列表的Append不满足交换律
l1 ++ l2 \= l2 ++ l2
-- 但数值的加法满足
1 + 2 == 2 + 1
```

## 高阶函数

### zip

1. 功能：将两个列表组合成元组列表

2. 参数：两个列表 `map list1 list2`
3. 返回值：两个列表各个对应位置匹配后的列表

```haskell
ghci> zip [1, 2] ['a', 'b']
[(1,'a'),(2,'b')]
-- 快速创建一个带索引的列表
ghci> zip [0..] ['a', 'b', 'c', 'd']
[(0,'a'),(1,'b'),(2,'c'),(3,'d')]

-- 两个列表如果长度不一样，以短的为准
ghci> zip [1] ['a', 'b']
[(1,'a')]
-- 由于以短的为准，所以 zip 一个空的列表还是空
ghci> zip [] [1..]
[]
```

### map

1. 功能：将指定函数应用到列表的每个元素，返回新列表
2. 参数：`map f l`

```haskell
-- 一个函数
ghci> f x = x^2
-- 一个列表
ghci> list = [1, 2, 3, 4, 5, 6]
-- 使用map
ghci> map f list
[1,4,9,16,25,36]

-- 另一种使用，封装基于map的函数
-- 直接定义一个不带参数的addOne函数，表示其使用map并将函数设置为默认执行一个匿名函数，将输入的每个元素+1
addOne :: [Int] -> [Int]
addOne = map (\x -> x + 1)
-- 结果
ghci> addOne [1, 2, 3, 4, 5]
[2,3,4,5,6]
```

### filter

1. 功能：遍历列表，使用断言函数判断该元素是否符合函数规则，最后输出新的为True的列表
2. 参数：`filter f l`

```haskell
-- 输出那些是奇数的元素组成的新列表
ghci> filter odd [1, 2, 3, 4, 5, 6, 7]
[1,3,5,7]

-- 另一种使用，封装基于filter的函数
-- 直接定义一个不带参数的函数，表示其使用filter并将断言函数设置为默认执行一个匿名函数，判断该元素是否大于零，输出所有大于零的元素列表
positiveFilter = filter (\x -> x > 0)
-- 结果
ghci> positiveFilter [-1, 4, 5, -7, 9, 0]
[4,5,9]
```

### foldr

1. 功能：对列表中每个元素接连执行指定运算符，来得到一个总的结果
2. 参数 `foldr f v l`
   - f 要执行的操作
   - v 如果列表为空，初始值设置为v
   - l 列表

```haskell
ghci> list = [1, 2, 3, 4, 5, 6, 7]
-- 对列表元素每个都累加，最后得到累加值
ghci> foldr (+) 0 list
28

-- 另一种使用，封装基于foldr的函数
-- 对列表每个元素执行累加，返回累加值；如果列表为空，默认返回 0
sumList :: [Int] -> Int
sumList = foldr (+) 0

--对列表每个元素执行累乘，返回累乘值；如果列表为空，默认返回 1
productList:: [Int] -> Int
productList = foldr (*) 1

-- 列表降维(推平)，将二维列表降维拼接成一维列表
concatenateList :: [[a]] -> [a]
concatenateList = foldr (++) []

-- 结果
ghci> sumList [1, 2, 3, 4, 5, 6]
21
ghci> productList [1, 2, 3, 4, 5, 6]
720
ghci> concatenateList [[1, 2], [3, 4], [5, 6]]
[1,2,3,4,5,6]
```

### 练习

```haskell
-- 9 算法：计算列表中所有正数的平方和
-- 方法一：使用列表推导
f1 :: [Int] -> Int
f1 l = sum [x^2 | x <- l, x > 0]
-- 方法二：使用列表的高级函数
-- (1) 先用filter筛选出l中所有正数，组成正数列表
-- (2) 再用map对正数列表执行平方操作，组成平方列表
-- (3) 最后使用foldr对平方列表进行累加
f2 :: [Int] -> Int
f2 l = foldr (+) 0 (map sqr (filter pos l))
    where pos x = x >0
          sqr x = x^2
-- 方法三：改良方法二，使用匿名函数来取代辅助函数
f3 :: [Int] -> Int
f3 l = foldr (+) 0 (map (\x -> x^2) (filter (\x -> x > 0) l))

-- 结果
ghci> f1 [-1, -3, -5, 0, 1, 6, 8, 3]
110
ghci> f2 [-1, -3, -5, 0, 1, 6, 8, 3]
110
ghci> f3 [-1, -3, -5, 0, 1, 6, 8, 3]
110
```



## 算法

### 数值类

1. 给定起始和终止，创建一个连续的整数列表
2. 计算给定数的阶乘

```haskell
-- 1 输入两个整数，一个是start，一个是finish，然后创建一个连续的整数列表
creatListFromTo :: Int -> Int -> [Int]
creatListFromTo x y
    -- 守卫：如果x > y，处理错误情况
    | x > y = []
    -- 如果x <= y，输出头部元素x，然后从x的下一个整数开始到y结束递归
    | x <= y = x : creatListFromTo (x + 1) y


-- 2 求一个数的阶乘
computingFactorial :: Int -> Int
-- 学习一种函数中定义辅助函数的方法，使用where关键词
-- 这里的 fact 辅助函数接受两个参数，起始数字为1，结束数字为x
computingFactorial x = fact 1 x
    where
        fact :: Int -> Int -> Int
        fact m n
            -- 守卫，处理错误情况
            | m > n = 1
            -- 如果m<=n，递归计算阶乘，m乘以从m的下一个整数开始到y结束递归
            | m <= n = m * fact (m + 1) n

-- 结果
ghci> creatListFromTo 1 10
[1,2,3,4,5,6,7,8,9,10]
ghci> computingFactorial 5
120
```

### 对应组合列表和按值查找

1. 将两个列表组合成元组列表
2. 按值查找列表中的元素，返回所有一样元素的索引列表

```haskell
-- 3 将两个列表组合成元组列表
-- 注意：也可以直接使用高阶库函数 zip list1 list2
listZip :: [a] -> [b] -> [(a, b)]
-- 处理错误情况，空列表
-- 另一个作用是当一个列表长度不够时终止递归
listZip [] l2 = []
listZip l1 [] = []
-- 将它们的头部元素组合成一个元组 (x,y)，并将该元组添加到结果列表的头部。然后递归地对剩余的列表部分调用 zip 函数，继续进行组合。
listZip (x : xs) (y : ys) = (x, y) : listZip xs ys


-- 4 按值查找列表中的元素，返回所有一样元素的索引列表
-- 该函数接受一个列表和一个值，Eq a ，是一个类型约束，表示类型 a 必须支持相等性比较
searchValue :: Eq a => [a] -> a -> [Int]
searchValue l v = srch l v 0
    where
        -- 函数中定义了一个辅助函数srch，参数：列表，搜寻的值，索引计数器(从0开始计数)
        srch :: Eq a => [a] -> a -> Int -> [Int]
        -- 如果别表为空，直接返回空列表
        srch [] v i = []
        -- 如果不为空，分割列表为当前头部元素和剩余列表
        srch (x : xs) v i
            -- 如果当前头部元素是要找的值，将其索引添加到输出列表中，然后在剩余列表中继续递归，找找还有没有
            | x == v = i : srch xs v (i + 1)
            -- 其他情况，在剩余列表中继续递归，继续找
            | otherwise = srch xs v (i + 1)


-- 5 结合高阶库函数zip使用列表推导来按照值查找
searchComp :: Eq a => [a] -> a -> [Int]
-- 首先将索引列表[0..]与输入列表zip匹配
-- 然后抽取匹配后的列表的每个元素，如果该元素值等于v，那么输出该元素的索引到输出列表中
searchComp l v = [ i | (i, x) <- zip [0..] l, x == v ]

-- 结果
ghci> listZip [1, 2, 3, 4, 5, 6] ['a', 'b', 'c', 'd']
[(1,'a'),(2,'b'),(3,'c'),(4,'d')]

ghci> list = [1, 2, 3, 4 ,5, 6, 7, 8, 9]
ghci> searchValue list 6
[5]
ghci> searchValue list 0
[]

ghci> list = ['a', 'b', 'c', 'd']
ghci> searchComp list 'c'
[2]
ghci> searchComp list 'f'
[]
```



## 文件操作

### 编写 .sh 文件

使用 VSCode 或者其他编辑器创建 .sh 文件，写代码

```haskell
main :: IO ()

-- 1 函数，提取列表中所有奇数
getOddsInList :: [Int] -> [Int]
getOddsInList l = [x | x <- l, odd x]
-- 2 函数：列表所有元素求平方
getSquaresInList :: [Int] -> [Int]
getSquaresInList l = [x^x | x <- l]

main = do
    -- 打印字符串
    putStrLn("求 1 到 n 中所有奇数的平方和：n =")
    -- 获取输入字符串
    inputString <- getLine
    -- 变量num: 将输入中的整数赋值给num
    let num = read inputString :: Int

    -- 2 函数二，提取列表中所有奇数每个求平方，再求和
    print(sum(getSquaresInList(getOddsInList [1..num])))
```

### 加载文件

1. 在该文件当前目录右键 选择 **在终端打开**
2. 输入 `ghci` 进入到 ghci
3. 输入 `:l 文件名.hs` 加载文件
4. 输入 `main` 执行主函数块

```haskell
-- 输入 `ghci` 进入到 ghci
D:\Study\Cardiff University\haskell>ghci
GHCi, version 9.4.8: https://www.haskell.org/ghc/  :? for help

-- 输入 `:l 文件名.hs` 加载文件
ghci> :l 02list.hs
[1 of 2] Compiling Main             ( 02list.hs, interpreted )
Ok, one module loaded.

-- 输入 `main` 执行主函数块
ghci> main
求 1 到 n 中所有奇数的平方和：n =
5
3153
```
